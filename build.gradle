defaultTasks "run" // default: gradle run

buildscript {
    ext.kotlin_version = "1.1.2-2"
    ext.jvm_target = "1.8"
    ext.gradle_version = "3.5"
    ext.junit_version  = "4.12"
    ext.tornadofx_version  = "1.7.4"
    ext.javacv_version  = "1.3.2"

    repositories {
        mavenCentral()
    }
    dependencies {
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
    }
}

apply plugin: "kotlin"
apply plugin: "application"

mainClassName = "com.x0.hatonekoe.ApplicationKt"

repositories {
    mavenCentral()
}

dependencies {
    compile "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"
    compile "org.jetbrains.kotlin:kotlin-reflect:$kotlin_version"
    compile "no.tornado:tornadofx:$tornadofx_version"
    compile "org.bytedeco:javacv-platform:$javacv_version"
    testCompile "org.jetbrains.kotlin:kotlin-test:$kotlin_version"
    testCompile "org.jetbrains.kotlin:kotlin-test-junit:$kotlin_version"
    testCompile "junit:junit:$junit_version"
}

compileKotlin {
    kotlinOptions.jvmTarget = "$jvm_target"
}

test {
    testLogging {
        exceptionFormat = "full" // display log details when fail
    }
}

task createJar(type: Jar, dependsOn: jar) {
    archiveName = project.name + ".jar"
    destinationDir = file("$rootDir/dist")
    manifest {
        attributes "Main-Class" : mainClassName
    }

    from configurations.compile.collect {it.isDirectory() ? it : zipTree(it)}
    from "$buildDir/classes/main"
    from "$buildDir/resources/main"
}

task wrapper(type: Wrapper) {
    gradleVersion = "$gradle_version"
}
